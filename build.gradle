import groovyx.net.http.HTTPBuilder
import groovyx.net.http.Method
import java.util.concurrent.CountDownLatch
import java.util.concurrent.TimeUnit
import java.util.concurrent.atomic.AtomicBoolean

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.2'
        classpath 'org.apache.httpcomponents:httpclient:4.5.2'
        classpath 'com.rbmh.rbss:rbss-platform-deploy-gradle-plugin:1.0.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id "net.saliman.cobertura" version "2.3.0"
    id 'idea'
    id "maven"
}

apply plugin: 'com.rbmh.rbss.rbss-platform-deploy-gradle-plugin'


ext {
    vertxVersion = '3.2.1'
    jsOutDir = "${projectDir}/build/generated"
    envList = ["staging", "preprod", "production"]
}

group 'com.redbullmediahouse.platform'
version '0.2.0'

sourceSets {
    generated {
        java.srcDir "${projectDir}/src/generated/java"
    }
}

idea {
    project {
        languageLevel = '1.8'
    }
}
repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'http://artifactory1.ops.prod.rbmhops.net:8081/artifactory/libs-release/' }
}

sourceCompatibility = '1.8'
mainClassName = 'com.redbullmediahouse.platform.discovery.DiscoveryVerticle'

dependencies {
    compile "ch.qos.logback:logback-core:1.1.3"
    compile "ch.qos.logback:logback-classic:1.1.3"
    compile "org.slf4j:slf4j-api:1.7.12"
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-lang-js:$vertxVersion"
    compile "io.vertx:vertx-codegen:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"
    compile "io.vertx:vertx-tcp-eventbus-bridge:$vertxVersion"
    compile "io.vertx:vertx-dropwizard-metrics:$vertxVersion"
    compile 'com.redbullmediahouse.platform:rbmh-config-utils:0.2.2'
    compile 'io.vertx:vertx-zookeeper:3.2.0'
    compile 'com.amazonaws:aws-java-sdk-ec2:1.10.51'
    testCompile "io.vertx:vertx-unit:$vertxVersion"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

/* used to create the fat jar files. */
jar {
    manifest {
        attributes(
                "Project-Version": project.version,
                "Project-Name": project.name
        )
    }
}

task generateProxies(type: JavaCompile, group: 'build', description: 'Generates the Vertx proxies') {
    doFirst {
        new File(jsOutDir).mkdirs()
    }
    source = sourceSets.main.java // input source set
    classpath = configurations.compile //+ configurations.vertx // add processor module to classpath
    // specify javac arguments
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor", // vertx processor here
            "-AoutputDirectory=${jsOutDir}"
    ]
    // specify output of generated code
    destinationDir = file("${projectDir}/src/generated/java")
}

compileJava {
    dependsOn(generateProxies)
    source += sourceSets.generated.java
}

task generateNodePackage() << {
    new File("${jsOutDir}/resources/rbmh-service-discovery-client-js/package.json").newWriter().withWriter { w ->
        w << """{
  "name": "$project.name",
  "version": "$project.version",
  "publishConfig": {"registry":"http://npm.rbmhops.net/artifactory/api/npm/rbmh-npm"}
}
"""
    }
}
generateNodePackage.dependsOn generateProxies

task copyNodePackage(type: Copy) {
    from "${jsOutDir}/resources/"
    into buildDir
}
copyNodePackage.dependsOn generateNodePackage

build.dependsOn copyNodePackage

task generateMesosDeploy() << {

    envList.each { deploymentEnv ->

        Properties props = new Properties()
        props.load(new FileInputStream("${project.rootDir}/deployment/${deploymentEnv}.properties"))

        new File("${buildDir}/mesos-deploy-${deploymentEnv}.json").newWriter().withWriter { w ->
            w << """{
  "container": {
    "type": "DOCKER",
    "docker": {
      "image": "rbmh-docker.docker.rbmhops.net/${project.name}:$project.version",
      "network": "BRIDGE",
      "portMappings": [
        {
          "containerPort": 8080,
          "hostPort": 0,
          "servicePort": 0,
          "protocol": "tcp"
        }
      ],
      "parameters": [
        {
          "key": "env",
          "value": "DEPLOYMENT_ENVIRONMENT=${deploymentEnv}"
        }
      ]
    }
  },
  "id": "${project.name}-${deploymentEnv}",
  "instances": ${props.instances},
  "cpus": ${props.cpus},
  "mem": ${props.mem},
  "uris": []
}
"""
        }
    }
}
generateMesosDeploy.dependsOn compileJava

task publishMesosDeploy() << {
    envList.each { deploymentEnv ->
        def http = new HTTPBuilder(
                "http://artifactory1.ops.prod.rbmhops.net:8081/artifactory/rbmh-mesos/" +
                        "${project.name}/${project.version}/deploy-${deploymentEnv}.json")
        http.request( Method.PUT, "application/json" ) { req ->
            body = file("${buildDir}/mesos-deploy-${deploymentEnv}.json").text
        }
    }
}
publishMesosDeploy.dependsOn generateMesosDeploy


def deployToStaging(String projectName, String projectVersion,
                    CountDownLatch countDownLatch, AtomicBoolean done) {

    println("Deploying $projectName $projectVersion to staging")

    def mesosDeploy = new URL( "http://artifactory1.ops.prod.rbmhops.net:8081/artifactory/rbmh-mesos/" +
            "${projectName}/${projectVersion}/deploy-staging.json" ).text
    def http = new HTTPBuilder(
            "http://mesos-master1.rbss.staging.rbmhops.net:8080/v2/apps")
    http.request(Method.POST, "application/json") { request ->
        body = mesosDeploy
        response.success = { response, reader ->

            if (response.statusLine.statusCode >= 200 && response.statusLine.statusCode < 299) {
                println("Deploy to staging $projectName $projectVersion worked with code $response.statusLine.statusCode " +
                        "${response.statusLine.reasonPhrase} ${reader.toString()}")

                done.set(true)
            }
            countDownLatch.countDown()

        }
    }
}


def deleteFromStaging(String projectName, String projectVersion,
                      CountDownLatch countDownLatch, AtomicBoolean done) {

    def mesosId = "${project.name}-staging"

    def deleteHttp = new HTTPBuilder(
            "http://mesos-master1.rbss.staging.rbmhops.net:8080/v2/apps/$mesosId")

    println("Deleting $mesosId from staging")
    deleteHttp.request(Method.DELETE) {
        response.success = { responseDelete, readerDelete ->
            if (responseDelete.statusLine.statusCode >= 200 && responseDelete.statusLine.statusCode < 299) {
                println("Call to delete $mesosId succeded with code $responseDelete.statusLine.statusCode " +
                        "${responseDelete.statusLine.reasonPhrase} ${readerDelete.toString()}" )
                deployToStaging(projectName, projectVersion, countDownLatch, done)
            } else {
                println("Call to delete $mesosId failed with code $responseDelete.statusLine.statusCode " +
                        "${responseDelete.statusLine.reasonPhrase} ${readerDelete.toString()}" )
                countDownLatch.countDown()
            }
        }
    }
}

task publishToMesosStaging() << {
    def mesosId = "${project.name}-staging"

    def checkDeployHttp = new HTTPBuilder(
            "http://mesos-master1.rbss.staging.rbmhops.net:8080/v2/apps/")

    CountDownLatch countDownLatch = new CountDownLatch(1);
    AtomicBoolean done = new AtomicBoolean();

    checkDeployHttp.request(Method.GET, "application/json") { req ->
        response.success = { response, reader ->
            if (reader.toString().contains(mesosId)) {
                println("Found mesosId $mesosId")
                deleteFromStaging(project.name, project.version, countDownLatch, done)
            } else {
                println("Did not find  mesosId $mesosId")
                deployToStaging(project.name, project.version, countDownLatch, done)
            }
        }
    }

    countDownLatch.await(30, TimeUnit.SECONDS)

    if (!done.get()) {

        println("We were unable to deploy to staging, check logs $mesosId")
        throw new IllegalStateException("We were unable to deploy to staging, check logs $mesosId")
    }

}
publishToMesosStaging.dependsOn publishMesosDeploy

task startTestDocker(type: Exec) {
    commandLine 'scripts/docker-start-test-containers.sh'
}
test.dependsOn startTestDocker

task stopTestDocker(type: Exec) {
    commandLine 'scripts/docker-stop-test-containers.sh'
}
test.finalizedBy stopTestDocker

test {
    useJUnit {
        includeCategories 'com.redbullmediahouse.platform.UnitTests'
    }
}

task itest(type: Test) {
    useJUnit {
        includeCategories 'com.redbullmediahouse.platform.IntegrationTests'
    }
}
itest.finalizedBy stopTestDocker
itest.dependsOn startTestDocker

task generateDockerFile() << {
    new File("${buildDir}/Dockerfile").newWriter().withWriter { w ->
        w << """
FROM vertx/vertx3-exec
MAINTAINER Geoff Chandler "geoff.chandler@us.redbullmediahouse.com"
ADD distributions/${project.name}-${project.version}.tar /
EXPOSE 8080
ENTRYPOINT ["sh", "-c"]
CMD ["/${project.name}-${project.version}/bin/${project.name}"]
"""
    }
}
generateDockerFile.dependsOn jar

task buildDocker(type: Exec) {
    commandLine 'scripts/docker-build.sh'
}
buildDocker.dependsOn generateDockerFile
buildDocker.dependsOn distTar

task pushDocker(type: Exec) {
    commandLine 'scripts/docker-push.sh'
}
pushDocker.dependsOn buildDocker

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
