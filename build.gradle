buildscript {
    repositories {
        mavenLocal()
        maven { url = 'http://artifactory1.ops.prod.rbmhops.net:8081/artifactory/plugins-release/' }
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath 'com.redbullsoundselect.platform:rbss-devops-plugin:1.1.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id "net.saliman.cobertura" version "2.3.0"
    id 'idea'
    id "maven"
}

apply plugin: 'rbss-devops'

ext {
    vertxVersion = '3.2.1'
    jsOutDir = "${projectDir}/build/generated"
}

group 'com.redbullsoundselect.platform'
version '0.4.0'
//version '0.3.' + new Date().format('yyyyMMddHHmmss')

sourceSets {
    generated {
        java.srcDir "${projectDir}/src/generated/java"
    }
}

idea {
    project {
        languageLevel = '1.8'
    }
}
repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'http://artifactory1.ops.prod.rbmhops.net:8081/artifactory/libs-release/' }
}

sourceCompatibility = '1.8'
mainClassName = 'com.redbullsoundselect.platform.discovery.DiscoveryVerticle'

dependencies {
    compile "ch.qos.logback:logback-core:1.1.3"
    compile "ch.qos.logback:logback-classic:1.1.3"
    compile "org.slf4j:slf4j-api:1.7.12"
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-lang-js:$vertxVersion"
    compile "io.vertx:vertx-codegen:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"
    compile "io.vertx:vertx-tcp-eventbus-bridge:$vertxVersion"
    compile "io.vertx:vertx-dropwizard-metrics:$vertxVersion"
    compile 'com.redbullmediahouse.platform:rbmh-config-utils:0.2.2'
    compile 'io.vertx:vertx-zookeeper:3.2.0'
    compile 'com.amazonaws:aws-java-sdk-ec2:1.10.51'
    compile 'org.apache.curator:curator-client:2.10.0'
    compile 'org.apache.curator:curator-framework:2.10.0'
    compile 'org.apache.curator:curator-recipes:2.10.0'
    testCompile "io.vertx:vertx-unit:$vertxVersion"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

test {
    useJUnit {
        includeCategories 'com.redbullsoundselect.platform.UnitTests'
    }
}

testDockerContainers {
    httpd {
        publishAll true
        containerName "httpd"
        image "httpd"
    }
}

mesosEnvironments {
    staging {
        url "http://mesos-master1.rbss.staging.rbmhops.net:8080/v2/apps"
        instances 1
        cpus 1
        mem 256
        healthCheck()
    }
    preprod {
        url "http://mesos-master1.rbss.preprod.rbmhops.net:8080/v2/apps"
        instances 3
        cpus 1
        mem 256
        healthCheck()
    }
    production {
        url "http://mesos-master1.rbss.prod.rbmhops.net:8080/v2/apps"
        instances 3
        cpus 1
        mem 256
        healthCheck()
    }
}

compileJava.source += project.sourceSets.generated.java

task itest(type: Test) {
    useJUnit {
        includeCategories 'com.redbullsoundselect.platform.IntegrationTests'
    }
}
itest.finalizedBy stopTestDocker
itest.dependsOn startTestDocker

artifacts {
    archives sourcesJar
    archives javadocJar
}
