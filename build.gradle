buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

plugins {
    id 'java'
    id 'application'
    id "net.saliman.cobertura" version "2.3.0"
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'idea'
    id "maven"
}

ext {
    vertxVersion = '3.2.0'
    jsOutDir = "${projectDir}/build/generated"
}

apply plugin: 'docker'

group 'com.redbullmediahouse.platform'
version '0.2.0'

docker {
    baseImage "vertx/vertx3-exec"
    maintainer 'Geoff Chandler "geoff.chandler@us.redbullmediahouse.com"'
}

task buildDocker(type: Docker) {

    tagVersion = project.version
    push = Boolean.getBoolean("docker.push")
    applicationName = project.name

    addFile {
        from "${project.shadowJar.outputs.files.singleFile}"
        into "/opt/rbmh/"
    }
    exposePort 8080
    entryPoint = ["sh", "-c"]
    defaultCommand = ["java -jar /opt/rbmh/${project.name}-${project.version}-fat.jar"]

}

sourceSets {
    generated {
        java.srcDir "${projectDir}/src/generated/java"
    }
}

idea {
    project {
        languageLevel = '1.8'
    }
}
repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'http://artifactory1.ops.prod.rbmhops.net:8081/artifactory/libs-release/' }
}

sourceCompatibility = '1.8'
mainClassName = 'com.redbullmediahouse.platform.discovery.DiscoveryVerticle'

dependencies {
    compile "ch.qos.logback:logback-core:1.1.3"
    compile "ch.qos.logback:logback-classic:1.1.3"
    compile "org.slf4j:slf4j-api:1.7.12"
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-lang-js:$vertxVersion"
    compile "io.vertx:vertx-codegen:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"
    compile "io.vertx:vertx-tcp-eventbus-bridge:$vertxVersion"
    compile 'com.redbullmediahouse.platform:rbmh-config-utils:0.2.1'
    compile 'com.amazonaws:aws-java-sdk-ec2:1.10.51'
    testCompile "io.vertx:vertx-unit:$vertxVersion"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

/* used to create the fat jar files. */
shadowJar {
    classifier = 'fat'
    manifest {
        attributes(
                "Project-Version": project.version
        )
    }
}
build.dependsOn shadowJar

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

task startDocker(type:Exec) {
    commandLine 'scripts/init-docker.sh'
}
test.dependsOn startDocker

task stopDocker(type:Exec) {
    commandLine 'scripts/shutdown-docker.sh'
}
test.finalizedBy stopDocker

task generateProxies(type: JavaCompile, group: 'build', description: 'Generates the Vertx proxies') {
    doFirst {
        new File(jsOutDir).mkdirs()
    }
    source = sourceSets.main.java // input source set
    classpath = configurations.compile //+ configurations.vertx // add processor module to classpath
    // specify javac arguments
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor", // vertx processor here
            "-AoutputDirectory=${jsOutDir}"
    ]
    // specify output of generated code
    destinationDir = file("${projectDir}/src/generated/java")
}

compileJava {
    dependsOn(generateProxies)
    source += sourceSets.generated.java
}

task generateNodePackage() << {
    new File("${jsOutDir}/resources/rbmh-service-discovery-client-js/package.json").newWriter().withWriter { w ->
        w << """{
  "name": "$project.name",
  "version": "$project.version",
  "publishConfig": {"registry":"http://npm.rbmhops.net/artifactory/api/npm/rbmh-npm"}
}
"""
    }
}
generateNodePackage.dependsOn generateProxies

task copyNodePackage(type: Copy) {
    from "${jsOutDir}/resources/"
    into buildDir
}
copyNodePackage.dependsOn generateNodePackage

build.dependsOn copyNodePackage

task generateMesosDeploy() << {
    new File("${buildDir}/mesos.deploy.json").newWriter().withWriter { w ->
        w << """{
    "container": {
        "type": "DOCKER",
        "docker": {
            "image": "rbmh-docker.docker.rbmhops.net/${project.name}:$project.version",
            "network": "BRIDGE",
            "portMappings": [
                {
                    "containerPort": 8080,
                    "hostPort": 0,
                    "servicePort": 0,
                    "protocol": "tcp"
                }
            ]
        }
    },
    "id": "${project.name}",
    "instances": 1,
    "cpus": ".25",
    "mem": 128,
    "uris": []
}
"""
    }
}

test {
    useJUnit {
        includeCategories 'com.redbullmediahouse.platform.UnitTests'
    }
}

task itest(type: Test) {
    useJUnit {
        includeCategories 'com.redbullmediahouse.platform.IntegrationTests'
    }
}
itest.finalizedBy stopDocker
itest.dependsOn startDocker

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
